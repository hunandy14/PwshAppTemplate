Bat轉接啟動PS
===

## 概念說明

使用 Bat 作為中間層來啟動 PowerShell 腳本，有以下優點：

1. **維持 PS1 的乾淨** - Log 紀錄邏輯完全由 Bat 層處理，PS1 只需專注於業務邏輯
2. **統一輸出處理** - 透過 `*>&1` 將 PowerShell 所有通道合併，再用 `Tee-Object` 同時輸出到畫面和檔案
3. **自動建立 Log 目錄** - Bat 負責檢查並建立 logs 資料夾
4. **時間戳記檔名** - 每次執行自動產生帶時間戳記的 log 檔案，方便追蹤

---

## PowerShell 輸出通道測試

用於驗證所有 PowerShell 輸出通道都能被正確捕捉：

```ps1
# PowerShell 所有通道輸出測試
Write-Output      "[PS通道1] Success     - Write-Output"
Write-Error       "[PS通道2] Error       - Write-Error"
Write-Warning     "[PS通道3] Warning     - Write-Warning"
Write-Verbose     "[PS通道4] Verbose     - Write-Verbose" -Verbose
Write-Debug       "[PS通道5] Debug       - Write-Debug" -Debug
Write-Host        "[PS通道6] Information - Write-Host"

# 外部進程測試
cmd /c "echo [外部進程] stdout - cmd echo"
cmd /c "echo [外部進程] stderr - cmd echo 1>&2"
```

| 通道 | 說明 | 對應指令 |
|------|------|----------|
| 1 | Success (標準輸出) | `Write-Output` |
| 2 | Error | `Write-Error` |
| 3 | Warning | `Write-Warning` |
| 4 | Verbose | `Write-Verbose -Verbose` |
| 5 | Debug | `Write-Debug -Debug` |
| 6 | Information | `Write-Information`, `Write-Host` |
| - | 外部程式 stdout | 外部程式標準輸出，併入通道 1 |
| - | 外部程式 stderr | 外部程式錯誤輸出，併入通道 2 |

---

## Bat 啟動腳本

```bat
@echo off & setlocal EnableDelayedExpansion
:: 設定變數
set "APP_NAME=.\App_Name.ps1"
set "LOG_DIR=%~dp0logs"
if not exist "%LOG_DIR%" mkdir "%LOG_DIR%"
:: 產生時間戳記
set "TSTAMP=%time: =0%"
set "DATESTAMP=%date:~0,4%%date:~5,2%%date:~8,2%"
set "TIMESTAMP=%TSTAMP:~0,2%%TSTAMP:~3,2%%TSTAMP:~6,2%"
set "LOG_FILE=%LOG_DIR%\%~n0_%DATESTAMP%-%TIMESTAMP%.log"
:: 執行 PowerShell 腳本
cd /d "%~dp0"
pwsh -exec bypass -noni -nop -c "&{ try{ &'%APP_NAME%' %*; exit($LASTEXITCODE) } catch{ $_; exit(1) } }*>&1 |tee '%LOG_FILE%'"
:: 結束
exit /b %ERRORLEVEL%
```

### 逐行解析

| 行 | 說明 |
|----|------|
| `APP_NAME=.\App_Name.ps1` | 設定要執行的 PS1 腳本路徑 |
| `LOG_DIR=%~dp0logs` | Log 目錄設為 Bat 所在目錄下的 `logs` 資料夾 |
| `if not exist ... mkdir` | 若 Log 目錄不存在則建立 |
| `TSTAMP=%time: =0%` | 取得時間並將空格補零（處理上午時段） |
| `LOG_FILE=...` | 組合 Log 檔名：`腳本名_YYYYMMDD-HHMMSS.log` |

### pwsh 參數說明

| 參數 | 說明 |
|------|------|
| `-exec bypass` | 略過執行原則檢查 |
| `-noni` | 非互動模式 |
| `-nop` | 不載入設定檔 |
| `-c "..."` | 執行指定命令 |

## 實際輸出範例

執行測試腳本後，Log 檔案內容如下：

```
[PS通道1] Success     - Write-Output
Write-Error: [PS通道2] Error       - Write-Error
[PS通道3] Warning     - Write-Warning
[PS通道4] Verbose     - Write-Verbose
[PS通道5] Debug       - Write-Debug
[PS通道6] Information - Write-Information
[PS通道6] Information - Write-Host
[外部進程] stdout - cmd echo
[外部進程] stderr - cmd echo
```

所有通道的輸出都被成功捕捉並寫入 Log 檔案。
